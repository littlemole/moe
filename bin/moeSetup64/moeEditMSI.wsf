

<job>
<!-- PUEditMSI.wsf

       o Word Count Summary has bit 3 set, and

       o TARGETDIR is in LocalAppDataFolder (Installer's name for
         CSIDL_LOCAL_APPDATA), and

       o ALLUSERS is null (doesn't exist).

     This allows installs of the application per-user w/o a UAC prompt
     under Vista.

-->

<script language="VBScript">
Option Explicit

dim arg 
arg = WScript.Arguments.Item(0)


dim installer
set installer = CreateObject("WindowsInstaller.Installer")

Const PID_WORDCOUNT = 15
Dim Database, SummaryInfo, View, Record, OldTarget, EndVar, NewTarget

Set Database = _
    Installer.OpenDatabase(arg,1) '  msiOpenDatabaseModeTransact)

Set SummaryInfo = Database.SummaryInformation(1)
With SummaryInfo
    .Property(PID_WORDCOUNT) = .Property(PID_WORDCOUNT) Or &H8&
    WScript.Echo "Word Count Summary now " & CStr(.Property(PID_WORDCOUNT))
    .Persist
End With

Set View = _
    Database.OpenView("SELECT Target FROM CustomAction" _
                    & " WHERE Action = 'DIRCA_TARGETDIR'" _
                    & " AND Source = 'TARGETDIR'")
View.Execute
Set Record = View.Fetch

If Record Is Nothing Then
    WScript.Echo "Missing TARGETDIR action, skipping"
    View.Close
    'WScript.Quit 1
Else
    OldTarget = Record.StringData(1)
    EndVar = InStr(OldTarget, "]") 'Char index of EndVar or 0.
    NewTarget = "[LocalAppDataFolder]" & Mid(OldTarget, EndVar + 1)
    Record.StringData(1) = NewTarget
    View.Modify 2, Record              ' msiViewModifyUpdate

    WScript.Echo "Target was """ & OldTarget _
               & """ and is now """ & NewTarget & """"
End If
View.Close

'WScript.Quit 0

Set View = _
    Database.OpenView("SELECT Property, Value FROM Property" _
                    & " WHERE Property = 'ALLUSERS'")
View.Execute
Set Record = View.Fetch

If Record Is Nothing Then
    WScript.Echo "ALLUSERS already omitted"
Else
    View.Modify 6, Record ' msiViewModifyDelete
    WScript.Echo "ALLUSERS deleted"
End If
View.Close

Database.Commit
</script>
</job>